/*
 * Root project configuration that is reused by subprojects to apply the Xtend compiler.
 */

repositories.jcenter()

configurations {
	xtendCompiler {
		description 'Bootstrap dependencies for the Xtend compiler'
		resolutionStrategy {
			eachDependency {
				if (requested.group == 'org.eclipse.xtext' || requested.group == 'org.eclipse.xtend')
					useVersion(versions.xtext_bootstrap)
				if (requested.group == 'com.google.inject' && requested.name == 'guice')
					useVersion(versions.guice)
			}
		}
		exclude group: 'asm'
	}
}

dependencies {
	xtendCompiler "org.eclipse.xtend:org.eclipse.xtend.core:$versions.xtext_bootstrap"
	xtendCompiler "org.xtext:xtext-gradle-builder:$versions.xtend_plugin"
}
